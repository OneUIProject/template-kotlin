plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

def locales = file('src/main/res').listFiles(new FilenameFilter() {
    @Override
    boolean accept(File dir, String name) {
        return name.matches('values-.*') && new File(dir, name).list().contains('strings.xml')
    }
}).collect { it.name.substring(7) }

print("Locales: " + locales.join(', '))

android {
    namespace 'oui.template.kotlin' /*TODO: change packageName*/
    compileSdk 33

    defaultConfig {
        applicationId "oui.template.kotlin" /*TODO: change packageName*/
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "1.0.0"

        resConfigs locales

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

configurations.all {
    exclude group: 'androidx.appcompat', module: 'appcompat'
    exclude group: 'androidx.core', module: 'core'
    exclude group: 'androidx.drawerlayout', module: 'drawerlayout'
    exclude group: 'androidx.viewpager', module: 'viewpager'
    exclude group: 'androidx.fragment', module: 'fragment'
    exclude group: 'androidx.customview', module: 'customview'
    exclude group: 'androidx.coordinatorlayout', module: 'coordinatorlayout'
    exclude group: 'com.android.support', module: 'support-compat'
}

dependencies {
    implementation 'androidx.core:core-ktx:1.10.0'

    implementation 'io.github.oneuiproject:design:1.2.3'
    implementation 'io.github.oneuiproject:icons:1.0.1'
    implementation 'io.github.oneuiproject.sesl:appcompat:1.3.0'
    implementation 'io.github.oneuiproject.sesl:material:1.4.0'
    implementation 'io.github.oneuiproject.sesl:recyclerview:1.3.0'
    implementation 'io.github.oneuiproject.sesl:preference:1.1.0'
    /*TODO: add/update/remove libraries*/
}